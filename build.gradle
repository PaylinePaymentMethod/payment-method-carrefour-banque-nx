import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.api.tasks.compile.JavaCompile

plugins {
    id "com.github.johnrengelman.shadow" version "7.1.0"
    id "org.sonarqube" version "3.3"
    id "org.owasp.dependencycheck" version "6.4.1.1"
}

apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply plugin: 'jacoco'

sourceCompatibility = 1.14
targetCompatibility = 1.14

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.incremental = true  // Pour accÃ©lerer la compilation
}

dependencies {
    implementation group: 'com.payline', name: 'payment-method-api', version: paymentMethodApiVersion

    compileOnly group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jsVersion
    compileOnly group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4jsVersion
    implementation group: 'com.github.scribejava', name: 'scribejava-core', version: scribejavaVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: httpComponentVersion
    implementation group: 'org.mapstruct', name: 'mapstruct', version: mapstructVersion
    annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: mapstructVersion
    implementation group: 'eu.bitwalker', name: 'UserAgentUtils', version: userAgentUtilsVersion

    testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: mockitoVersion
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jsVersion
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4jsVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: jupiterVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: jupiterVersion

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion


    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    compileOnly group: 'org.projectlombok', name: 'lombok-mapstruct-binding', version: lombokMapstructBindingVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok-mapstruct-binding', version: lombokMapstructBindingVersion
    testCompileOnly group: 'org.projectlombok', name: 'lombok-mapstruct-binding', version: lombokMapstructBindingVersion
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok-mapstruct-binding',version: lombokMapstructBindingVersion

}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-JDK': System.getProperty('java.version'),
                'Build-Date': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Source-Compatibility': project.sourceCompatibility,
                'Target-Compatibility': project.targetCompatibility
        )
    }
}


assemble.dependsOn shadowJar

shadowJar {
    classifier = ''
    dependencies {
        exclude(dependency("com.payline:payment-method-api"))
        exclude(dependency("org.apache.logging.log4j:log4j-api:$log4jsVersion"))
        exclude(dependency("org.apache.logging.log4j:log4j-core:$log4jsVersion"))

        exclude(dependency("org.junit.jupiter:junit-jupiter-params"))
        exclude(dependency("org.junit.jupiter:junit-jupiter-api"))
    }
}

processResources {
    // Replace tokens in release.properties file by their real value
    filesMatching('release.properties') {
        filter(ReplaceTokens, tokens: [
                'version': project.version.replaceFirst("-.*", ""),
                'date'   : new Date().format("dd/MM/yyyy")
        ])
    }
}

test {
    useJUnitPlatform()
    exclude('**/*IT.class')
}

dependencyCheck {
    format 'ALL'
    skipConfigurations = ['jacocoAgent', 'jacocoAnt', 'annotationProcessor', 'axisGenAntTask', 'jaxws', 'execJar', 'jaxb', 'generateSources']
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

repositories {
    mavenLocal()
    maven {
        url nexusGroupsUrl
        credentials {
            username mavenUser
            password mavenPassword
        }
    }
    maven {
        url nexusSnapshotUrl
        credentials {
            username mavenUser
            password mavenPassword
        }
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
    repositories {
        maven {
            if (project.version.endsWith('-SNAPSHOT')) {
                url nexusSnapshotUrl
            } else {
                url nexusPublicUrl
            }
            credentials {
                username mavenUser
                password mavenPassword
            }
        }
    }
}



plugins.withType(JacocoPlugin) {
    tasks["test"].finalizedBy 'jacocoTestReport'
}

sonarqube {
    properties {
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.dependencyCheck.reportPath", "${project.buildDir}/reports/dependency-check-report.xml"
        property 'sonar.dependencyCheck.htmlReportPath', "${project.buildDir}/reports/dependency-check-report.html"
    }
}

task printVersion {
    doLast {
        println version
    }
}